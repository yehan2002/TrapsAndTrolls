
plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
     id "com.github.johnrengelman.shadow" version "4.0.2"
}

import org.apache.tools.ant.filters.ReplaceTokens

group = 'io.github.yehan2002.Traps'
version = "1.7" //$VERSION$
archivesBaseName = 'Traps'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def bukkitVersion = '1.17.1-R0.1-SNAPSHOT'

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12

    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1

    if(isCommit) return head[0].trim().take(takeFromHash)

    def refHead = new File(gitFolder + head[1].trim())
    refHead.text.trim().take takeFromHash
}

repositories {
    jcenter()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven{
        url 'https://jitpack.io'
    }

    maven{
        url 'https://repo.codemc.org/repository/maven-public'
    }

    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile (group: 'org.spigotmc', name: 'spigot-api', version: bukkitVersion)
    compile(group: 'com.github.MilkBowl', name: 'VaultAPI', version: '1.7')
    compile (group: 'de.tr7zw', name: 'item-nbt-api', version: '2.8.0')
}

version = version + "-SNAPSHOT-" + getCheckedOutGitCommitHash()

compileJava.options.encoding = 'UTF-8'

task fatJar(type: Jar) {
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
}

shadowJar{

    dependencies {
        exclude(dependency {it.moduleGroup != 'de.tr7zw'})
    }

    relocate 'de.tr7zw.changeme.nbtapi', 'io.github.yehan2002.Traps.nbtapi';

    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }

    classifier = ''

    from('src/') {
        include 'plugin.yml'
        include 'config.yml'
        include 'traps.yml'
        filter(ReplaceTokens, tokens: [VERSION : project.version])
    }
    doLast {
        def dir = new File("builds")

        if( !dir.exists() ) {
            dir.mkdirs()
        }
        
        copy {
            from 'build/libs'
            into "builds"
        }
    }

}

tasks.build {
    dependsOn(tasks.shadowJar)
}

tasks.jar {
    enabled = false
}
