
plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

import org.apache.tools.ant.filters.ReplaceTokens

group = 'io.github.yehan2002.Traps'
version = "1.7.1" //$VERSION$
archivesBaseName = 'Traps'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'


def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12

    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1

    if(isCommit) return head[0].trim().take(takeFromHash)

    def refHead = new File(gitFolder + head[1].trim())
    refHead.text.trim().take takeFromHash
}

repositories {
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven{
        url 'https://jitpack.io'
    }

    maven{
        url 'https://repo.codemc.org/repository/maven-public'
    }

    maven {
        url "https://plugins.gradle.org/m2/"
    }

    mavenCentral()
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    implementation 'de.tr7zw:item-nbt-api:2.8.0'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

version = version + "-" + getCheckedOutGitCommitHash()

compileJava.options.encoding = 'UTF-8'



shadowJar{
    relocate 'de.tr7zw.changeme.nbtapi', 'io.github.yehan2002.Traps.nbtapi';

    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }

    from('src/') {
        include 'plugin.yml'
        include 'config.yml'
        include 'traps.yml'
        filter(ReplaceTokens, tokens: [VERSION : project.version])
    }
    doLast {
        def dir = new File("builds")

        if( !dir.exists() ) {
            dir.mkdirs()
        }
        
        copy {
            from 'build/libs'
            into "builds"
        }
    }

}



sourceSets {
    main {
        java {
            srcDir 'src/'
        }
    }
}



tasks.build {dependsOn(tasks.shadowJar)}
