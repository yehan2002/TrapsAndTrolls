import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group = 'io.github.yehan2002.Traps'
version = '1.0'
archivesBaseName = 'Traps'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def bukkitVersion = '1.12.2-R0.1-SNAPSHOT'

repositories {
    jcenter()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }
}

dependencies {
    compile (group: 'org.spigotmc', name: 'spigot-api', version: bukkitVersion)
    compile (group: 'net.milkbowl.vault', name: 'VaultAPI', version: "1.7")
}

/* This will replace '@VERSION@' in the plugin.yml with the version declared above, just put '@VERSION@' for the version in the plugin.yml */
processResources {
    filter ReplaceTokens, tokens: [
            "VERSION": version
    ]
}


compileJava.options.encoding = 'UTF-8'

task fatJar(type: Jar) {

    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }


}


jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
    from('src/') {
        include 'plugin.yml'
    }
}


